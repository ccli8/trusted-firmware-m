#-------------------------------------------------------------------------------
# Copyright (c) 2020-2022, Arm Limited. All rights reserved.
# Copyright (c) 2020 Nuvoton Technology Corp. All rights reserved.
#
# SPDX-License-Identifier: BSD-3-Clause
#
#-------------------------------------------------------------------------------

cmake_policy(SET CMP0076 NEW)
set(CMAKE_CURRENT_SOURCE_DIR ${CMAKE_CURRENT_LIST_DIR})

#========================= Platform region defs ===============================#
target_include_directories(platform_region_defs
    INTERFACE
        partition
)

#========================= Platform common defs ===============================#

# Specify the location of platform specific build dependencies.
target_sources(tfm_s
    PRIVATE
        ${CMAKE_CURRENT_SOURCE_DIR}/device/source/startup_m2354.c
)
target_add_scatter_file(tfm_s
    $<$<AND:$<VERSION_LESS:${TFM_ISOLATION_LEVEL},3>,$<C_COMPILER_ID:ARMClang>>:${CMAKE_SOURCE_DIR}/platform/ext/common/armclang/tfm_common_s.sct>
    $<$<AND:$<VERSION_LESS:${TFM_ISOLATION_LEVEL},3>,$<C_COMPILER_ID:GNU>>:${CMAKE_SOURCE_DIR}/platform/ext/common/gcc/tfm_common_s.ld>
    $<$<AND:$<VERSION_LESS:${TFM_ISOLATION_LEVEL},3>,$<C_COMPILER_ID:IAR>>:${CMAKE_SOURCE_DIR}/platform/ext/common/iar/tfm_common_s.icf>
)

if(NS)
    target_sources(tfm_ns
        PRIVATE
            ${CMAKE_CURRENT_SOURCE_DIR}/device/source/startup_m2354.c
    )
    target_add_scatter_file(tfm_ns
        $<$<C_COMPILER_ID:ARMClang>:${CMAKE_CURRENT_SOURCE_DIR}/device/source/armclang/m2354_ns.sct>
        $<$<C_COMPILER_ID:GNU>:${CMAKE_CURRENT_SOURCE_DIR}/device/source/gcc/m2354_ns.ld>
        $<$<C_COMPILER_ID:IAR>:${CMAKE_CURRENT_SOURCE_DIR}/device/source/iar/m2354_ns.icf>
    )
    target_link_libraries(CMSIS_5_tfm_ns
        INTERFACE
            CMSIS_5_RTX_V8MBN
    )
endif()

if(BL2)
    target_sources(bl2
        PRIVATE
            ${CMAKE_CURRENT_SOURCE_DIR}/device/source/startup_m2354.c
            bsp/Library/StdDriver/src/sys.c
            bsp/Library/StdDriver/src/clk.c
    )

    target_add_scatter_file(bl2
        $<$<C_COMPILER_ID:ARMClang>:${CMAKE_CURRENT_SOURCE_DIR}/device/source/armclang/m2354_bl2.sct>
        $<$<C_COMPILER_ID:GNU>:${CMAKE_CURRENT_SOURCE_DIR}/device/source/gcc/m2354_bl2.ld>
        $<$<C_COMPILER_ID:IAR>:${CMAKE_CURRENT_SOURCE_DIR}/device/source/iar/m2354_bl2.icf>
    )
endif()

#========================= Platform Secure ====================================#

target_include_directories(platform_s
    PUBLIC
        .
        ../common/
        ../common/cmsis_drivers/config
        ../common/native_drivers
        partition
        device/include
        device/config
        bsp/Device/Nuvoton/M2354/Include
        bsp/Library/StdDriver/inc
        ../common/bsp/Include
        ../common/bsp/StdDriver/inc
        ${PLATFORM_DIR}/ext/cmsis
        ${PLATFORM_DIR}/ext/driver
)

target_sources(platform_s
    PRIVATE
        device/source/system_core_init.c
        device/source/device_definition.c
        ../common/cmsis_drivers/Driver_Flash.c
        ../common/cmsis_drivers/Driver_USART.c
        ../common/native_drivers/arm_uart_drv.c
        ../common/native_drivers/mpu_armv8m_drv.c
        ../common/native_drivers/timer_cmsdk_drv.c
        bsp/Library/StdDriver/src/timer.c
        bsp/Library/StdDriver/src/fmc.c
        bsp/Library/StdDriver/src/tamper.c
        bsp/Library/StdDriver/src/sys.c
        bsp/Library/StdDriver/src/clk.c
        bsp/Library/StdDriver/src/rtc.c
        $<$<BOOL:${NU_SDH_CMSIS_FLASH}>:${CMAKE_CURRENT_SOURCE_DIR}/../common/bsp/StdDriver/src/sdh.c>
        ../common/services/src/tfm_platform_system.c
        $<$<BOOL:${NU_SDH_CMSIS_FLASH}>:${CMAKE_CURRENT_SOURCE_DIR}/../common/cmsis_drivers/Driver_SDH_Flash.c>
        ${PLATFORM_DIR}/ext/common/uart_stdout.c
        $<$<BOOL:${TFM_PARTITION_PLATFORM}>:${CMAKE_CURRENT_SOURCE_DIR}/plat_test.c>
        $<$<BOOL:${TFM_PARTITION_PLATFORM}>:${CMAKE_CURRENT_SOURCE_DIR}/../common/services/src/platform_extra_secure.c>
        $<$<BOOL:${TFM_PARTITION_PLATFORM}>:${CMAKE_CURRENT_SOURCE_DIR}/../common/services/src/platform_extra_secure_guard.c>
)

target_compile_options(platform_s
    PUBLIC
        ${COMPILER_CMSE_FLAG}
)

target_compile_definitions(platform_s
    PUBLIC
        NU_TFM_PLAT_IOCTL_S=1
        $<$<BOOL:${NU_HXT_PRESENT}>:NU_HXT_PRESENT=1>
        $<$<BOOL:${NU_UPDATE_STAGE_SDH}>:NU_UPDATE_STAGE_SDH=1>
        $<$<BOOL:${NU_UPDATE_STAGE_SDH}>:NU_SDH_CMSIS_FLASH=1>
        $<$<BOOL:${NU_UPDATE_STAGE_SDH}>:NU_SDH_DIS_INTR=1>
        $<$<BOOL:${NU_UPDATE_STAGE_SDH}>:NU_SDH_ENA_TIMEOUT=1>
        $<$<BOOL:${NU_UPDATE_STAGE_SDH}>:NU_SDH_INIT_GUARD=1>    # Cover upper layer missing doing SDH initialization
        $<$<BOOL:${NU_UPDATE_STAGE_FLASH}>:NU_UPDATE_STAGE_FLASH=1>
)

#========================= Platform Non-Secure ================================#

target_sources(platform_ns
    PRIVATE
        ../common/native_drivers/arm_uart_drv.c
        $<$<BOOL:${TFM_PARTITION_PLATFORM}>:${CMAKE_CURRENT_SOURCE_DIR}/../common/native_drivers/timer_cmsdk_drv.c>
        ../common/cmsis_drivers/Driver_USART.c
        device/source/device_definition.c
        device/source/system_core_init.c
        $<$<BOOL:${TFM_PARTITION_PLATFORM}>:${CMAKE_CURRENT_SOURCE_DIR}/plat_test.c>
        $<$<BOOL:${TFM_IRQ_TEST}>:${CMAKE_CURRENT_SOURCE_DIR}/bsp/Library/StdDriver/src/timer.c>
        ../common/services/src/platform_extra_secure.c
        ../common/services/src/platform_extra_secure_compat.c
)

target_include_directories(platform_ns
    PUBLIC
        .
        ../common/
        ${PLATFORM_DIR}/..
        device/config
        device/include
        ../common/native_drivers
        bsp/Device/Nuvoton/M2354/Include
        bsp/Library/StdDriver/inc
        ../common/bsp/StdDriver/inc
        ../common/bsp/Include
        ../common/bsp/StdDriver/inc
        ../common/cmsis_drivers/config
        ../common/services/src/
)

target_compile_definitions(platform_ns
    PUBLIC
        NU_TFM_PLAT_IOCTL_NS=1
        $<$<BOOL:${NU_HXT_PRESENT}>:NU_HXT_PRESENT=1>
)

#========================= Platform BL2 =======================================#

if(BL2)
    target_sources(platform_bl2
        PRIVATE
            device/source/device_definition.c
            device/source/system_core_init.c
            ../common/cmsis_drivers/Driver_Flash.c
            ../common/native_drivers/arm_uart_drv.c
            ../common/cmsis_drivers/Driver_USART.c
            bsp/Library/StdDriver/src/fmc.c
            bsp/Library/StdDriver/src/tamper.c
            $<$<BOOL:${NU_SDH_CMSIS_FLASH}>:${CMAKE_CURRENT_SOURCE_DIR}/../common/bsp/StdDriver/src/sdh.c>
            $<$<BOOL:${NU_SDH_CMSIS_FLASH}>:${CMAKE_CURRENT_SOURCE_DIR}/../common/cmsis_drivers/Driver_SDH_Flash.c>
    )
    target_include_directories(platform_bl2
        PUBLIC
            partition
            bsp/Device/Nuvoton/M2354/Include
            bsp/Library/StdDriver/inc
            ../common/bsp/Include
            ../common/bsp/StdDriver/inc
            device/include
            device/config
            ../common/cmsis_drivers/config
        PRIVATE
            .
            ../common/
            ${PLATFORM_DIR}/..
            ../common/native_drivers
    )
    target_compile_options(platform_bl2
        PUBLIC
            ${COMPILER_CMSE_FLAG}
    )
    target_compile_definitions(platform_bl2
        PUBLIC
            $<$<BOOL:${NU_HXT_PRESENT}>:NU_HXT_PRESENT=1>
            $<$<BOOL:${NU_UPDATE_STAGE_SDH}>:NU_UPDATE_STAGE_SDH=1>
            $<$<BOOL:${NU_UPDATE_STAGE_SDH}>:NU_SDH_CMSIS_FLASH=1>
            $<$<BOOL:${NU_UPDATE_STAGE_SDH}>:NU_SDH_DIS_INTR=1>
            $<$<BOOL:${NU_UPDATE_STAGE_SDH}>:NU_SDH_ENA_TIMEOUT=1>
            $<$<BOOL:${NU_UPDATE_STAGE_SDH}>:NU_SDH_DUMMY_ON_ABSENT=1>   # Simulate dummy SD card on detached because MCUboot will panic on read failure
            $<$<BOOL:${NU_UPDATE_STAGE_FLASH}>:NU_UPDATE_STAGE_FLASH=1>
        PRIVATE
            $<$<BOOL:${BL2}>:BL2>
    )
endif()

#========================= tfm_spm ============================================#

target_sources(tfm_spm
    PRIVATE
        target_cfg.c
        ../common/tfm_hal_isolation.c
        ../common/tfm_hal_platform.c
        ../common/faults.c
)
